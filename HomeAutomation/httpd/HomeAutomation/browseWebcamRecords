#!/usr/local/bin/perl
#Copyright (c) 2014 by Wayne Wright, Round Rock, Texas.
#See license at http://github.com/w5xd/diysha/blob/master/LICENSE.md
# cgi script that presents webcam recordings for browsing and playing

use strict;
use warnings;
use HomeAutomation::WebcamRecordConfig;
use CGI;

my $gapToBreakMovieSeconds = 4;

my $q = CGI->new;

# Process an HTTP request
# Prepare various HTTP responses
print $q->header();
print $q->header('text/html');
print $q->start_html( "-title" => "Webcam Recording Browser" );
my $v = $q->param('webcam');
if ( !defined($v) ) { print $q->end_html("<p>no webcam</p>"); exit 0; }
my $f = HomeAutomation::WebcamRecordConfig->new($v);
if ( !defined($f) ) { print $q->end_html("<p>invalid webcam</p>"); exit 0; }

my $loc = $f->getLocation();
opendir( my $dh, $loc ) or die $!;
my %files;
while ( readdir $dh ) {
    my $thisFile = $_;
    next if !( $thisFile =~ /\.jpg$/i );
    next if ( -d $thisFile );
    my $t = $f->getTime($thisFile);
    $files{$thisFile} = $t;
}
closedir $dh;

my @fnameSort = sort {(stat "$loc/$a")[9] <=> (stat "$loc/$b")[9]} keys %files;
my $prevTm;
my $filesInMovie = 0;
my $movieName;
my $startTime;
my $printedDate;
for (@fnameSort) {
    my $fn = $_;
    my $tm = $files{$fn};
    my $df = 1 + $gapToBreakMovieSeconds;
    $df = $tm - $prevTm if defined $prevTm;
    if ( $df > $gapToBreakMovieSeconds ) {
        $printedDate =
          &printMovieLine( $v, $movieName, $filesInMovie, $startTime,
            $printedDate )
          if $filesInMovie;
        $movieName    = $fn;
        $startTime    = $tm;
        $filesInMovie = 0;
    }
    $filesInMovie += 1;
    $prevTm = $tm;
}
printMovieLine( $v, $movieName, $filesInMovie, $startTime, $printedDate )
  if ($filesInMovie);
print $q->end_html;

sub printMovieLine {
    my $wc           = shift;
    my $movieName    = shift;
    my $filesInMovie = shift;
    my $st           = shift;
    my $pd           = shift;
    if (   !defined($pd)
        || ( $st->mday != $pd->mday )
        || ( $st->mon != $pd->mon )
        || ( $st->year != $pd->year ) )
    {
        print $st->strftime("<b>%Y %B %e</b><br/>");
        $pd = $st;
    }
    print "<a href='playWebcamRecords?webcam=$wc"
      . "&amp;start=$movieName&amp;numFiles=$filesInMovie'>"
      . $st->strftime("%H:%M:%S")
      . "($filesInMovie)</a><br/>\n";
    return $pd;
}

